plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

archivesBaseName = 'modulo-api'
sourceCompatibility = 11

dependencies {
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3'
    implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:9.0.39'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.mockito:mockito-core:4.5.1'
}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = 'Modulo is a collaborative Java framework, where anybody can add any module to his Modulo Server and developeur can create easily and quickly create their own module with our API.'
                url = 'https://modulo.chillycheesy.com'
                licenses {
                    license {
                        name = 'Mizilla Public License, Version 2.0'
                        url = 'https://www.mozilla.org/en-US/MPL/2.0/'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ChillyCheesy/Modulo.git'
                    developerConnection = 'scm:git:git@github.com:ChillyCheesy/Modulo.git'
                    url = 'https://github.com/ChillyCheesy/Modulo/tree/master'
                }
            }
        }
    }
}

